#Область ОбработчикиСобытий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если не Параметры.Свойство("SSCC") тогда
		Отказ=Истина;
		Возврат
	КонецЕсли;
	SSCC=Параметры.SSCC;
	ВидимостьИДоступностьЭлементовСервер();
КонецПроцедуры
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбщийМодульКлиентскойЧасти.ПереопределенияТригераПолучениеШтрихКодаПриОткрытии(ЭтаФорма);
	СкопироватьДанныеSSCCЯчейкиФормыВладельцаНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ОбщийМодульКлиентскойЧасти.ПереопределенияТригераПолучениеШтрихКодаПриЗакрытии(ЭтаФорма);
КонецПроцедуры
&НаКлиенте
Процедура СоставЯчейкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.СоставЯчейки.ТолькоПросмотр тогда
		СтандартнаяОбработка=Ложь;
		ОткрытьФорму("Обработка.ОбработчикИнвентаризацииЯчейки.Форма.ФормаПросмотраСтрокиНМ",новый Структура("НоменклатураПредставление,"
		+"ДатаРозлива,КоличествоПлан,КоличествоФакт",ТекущийЭлемент.ТекущиеДанные.НоменклатураПредставление,
		ТекущийЭлемент.ТекущиеДанные.ДатаРозлива,ТекущийЭлемент.ТекущиеДанные.Количество,ТекущийЭлемент.ТекущиеДанные.КоличествоФакт),ЭтаФорма);
		ТригерПриемаСканераШтрихКода=Ложь;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиКоманд
&НаКлиенте
Процедура Назад(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	КИзменению=Истина;
	ВидимостьИДоступностьЭлементовКлиент();
КонецПроцедуры
&НаКлиенте
Процедура Удалить(Команда)
	Если ТекущийЭлемент.Имя="СоставЯчейки" тогда
		ИдентификаторСтроки=ТекущийЭлемент.ТекущиеДанные.ПолучитьИдентификатор();
		Строка = СоставЯчейки.НайтиПоИдентификатору(ИдентификаторСтроки);
		ИдСтрокиКУдалению=Строка.ИдСтроки;
		СоставЯчейки.Удалить(Строка);
		СписокУдаленныхСтрок.Добавить(ИдСтрокиКУдалению);
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура Подтвердить(Команда)
	для Каждого стр из СоставЯчейки Цикл 
		стр.КоличествоФакт=стр.Количество;
	КонецЦикла;	
	ПередатьДанныеВладельцу();
КонецПроцедуры
&НаКлиенте
Процедура ПодтвердитьИзменения(Команда)
	ПередатьДанныеВладельцу();
КонецПроцедуры
&НаКлиенте
Процедура ПередатьДанныеВладельцу()
	для Каждого стр из СоставЯчейки цикл
		МассивСтрок=ВладелецФормы.СоставЯчейки.НайтиСтроки(новый Структура("ИдСтроки",стр.ИдСтроки));
		Если МассивСтрок.Количество()>0 тогда
			МассивСтрок[0].КоличествоФакт=стр.КоличествоФакт;
		КонецЕсли;
	КонецЦикла;
	для Каждого ЭлементаСписка из СписокУдаленныхСтрок цикл
		МассивСтрокУдаления=ВладелецФормы.СоставЯчейки.НайтиСтроки(новый Структура("ИдСтроки",ЭлементаСписка.Значение));
		для Каждого Строка из МассивСтрокУдаления цикл
			ВладелецФормы.СоставЯчейки.Удалить(Строка);
		КонецЦикла;
	КонецЦикла;
	ВладелецФормы.ОбработкаСоставЯчейкиБезУчетаSSCCКлиент();
	ЭтаФорма.Закрыть();
КонецПроцедуры
&НаКлиенте
Процедура УдалитьSSCCОповещение(Результат,Параметры) Экспорт 
	Если Результат=КодВозвратаДиалога.Нет Тогда 
		Возврат
	КонецЕсли;
	для Каждого стр из СоставЯчейки цикл
		МассивСтрок=ВладелецФормы.СоставЯчейки.НайтиСтроки(новый Структура("ИдСтроки",стр.ИдСтроки));
		для Каждого Строка из МассивСтрок цикл
			ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
			СтрокаКУдалению = ВладелецФормы.СоставЯчейки.НайтиПоИдентификатору(ИдентификаторСтроки);
			ВладелецФормы.СоставЯчейки.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЦикла;
	для Каждого ЭлементаСписка из СписокУдаленныхСтрок цикл
		МассивСтрокУдаления=ВладелецФормы.СоставЯчейки.НайтиСтроки(новый Структура("ИдСтроки",ЭлементаСписка.Значение));
		для Каждого СтрокаУдаления из МассивСтрокУдаления цикл
			ВладелецФормы.СоставЯчейки.Удалить(СтрокаУдаления);
		КонецЦикла;
	КонецЦикла;
    ВладелецФормы.ОбработкаСоставЯчейкиБезУчетаSSCCКлиент();

	ЭтаФорма.Закрыть();
	
КонецПроцедуры
&НаКлиенте
Процедура УдалитьSSCC(Команда)	
	Оповещение=новый ОписаниеОповещения("УдалитьSSCCОповещение",ЭтаФорма);
	ПоказатьВопрос(Оповещение,"Вы точно хотите удалить все данные текущего SSCC",РежимДиалогаВопрос.ДаНет);
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ВидимостьИДоступностьЭлементовКлиент()
	ВидимостьИДоступностьЭлементовСервер();
КонецПроцедуры
&НаСервере
Процедура ВидимостьИДоступностьЭлементовСервер()
	Элементы.ПодтвердитьИзменения.Видимость= КИзменению;
	Элементы.СоставЯчейки.ТолькоПросмотр=не КИзменению;
	Элементы.Подтвердить.Видимость= не КИзменению;
	Элементы.СоставЯчейкиКонтекстноеМенюУдалить.Видимость=КИзменению;
КонецПроцедуры
&НаКлиенте
Процедура СкопироватьДанныеSSCCЯчейкиФормыВладельцаНаКлиенте()
	СтрокиТаблицыВладельца=ВладелецФормы.СоставЯчейки.НайтиСтроки(новый Структура("ИдентификаторУпаковки",SSCC));
	для Каждого стр из СтрокиТаблицыВладельца цикл
		НоваяСтрока=СоставЯчейки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,стр);
	КонецЦикла;
КонецПроцедуры



#КонецОбласти


